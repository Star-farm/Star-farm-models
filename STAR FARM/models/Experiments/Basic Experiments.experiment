/**
* Name: BasicExperiments
* This wizard creates a new experiment file. 
* Author: patricktaillandier
* Tags:  
*/ 

model STARFARM  

import "../Global.gaml"   

 
global{
	string economy_display_type <- "Income";
	int overlay_font_size <- 12;
	font overlay_font <- font("SansSerif", overlay_font_size, #bold);
	string show_season <- first(possible_practices.keys);
	int season_opacity <- 100;
	string time_range_type <- "All";
	map<string,unknown> options <- [];
	string scale <- "Total";
	
	// List of the key indicators to be monitored
	// the next list should be initialized with key_indicators from Practices.gaml, but presently there is a bug with Gama	
	list<string> chart_list <- ["Harvest","Profit","Water consumption","Fertilizer consumption","Adoption rate","Expenses details"];
//	list<string> chart_list <- ["Seasons","Total crop biomass per practice","Avg crop biomass per practice","Surface per practice","Adoption rates"];
	string current_chart <- first(chart_list);
	
	
	
	// ======================================================================
	// indicators
	// ======================================================================
	
	// computes the total biomass for a given type of crop and practice
	float get_total_biomass_by_practice(Crop_practice p){
		return Crop where(each.the_farmer.practice = p) sum_of(each.B);
	}
	
	// computes the total crop area for a given practice
	float get_cultivated_area_by_practice(Crop_practice p){
		return Crop where(each.the_farmer.practice = p) sum_of(each.concerned_plot.shape.area);
	}
	
	// computes the average biomass for a given type of crop and practice
	float get_avg_biomass_by_practice(Crop_practice p){
		float total_area <- get_cultivated_area_by_practice(p);
		return total_area=0 ? 0:get_total_biomass_by_practice(p)/total_area;
	}
	
	
	
	// ======================================================================
	// auxiliary functions for charts
	// ======================================================================
	
	// compute the time range for charts, i.e. the number of days for which charts should be displayed
	int time_range(string s){
		int range <- max(1,cycle);
		switch s {
			match "Last year" {return min(365,range);}
			match "Last 3 years" {return min(1095,range);}
			default {return range;}
		}
	}
	
	// make x labels for season histograms
	list<string> update_x_labels(Crop_practice p){
		list<string> tmp;
		ask first(practices){
			tmp <- create_x_labels();
		}		 
		return tmp collect(each+p.id);
	}
	
	

	// ======================================================================
	// functions to build yearly histograms
	// ======================================================================
	 
	// functions to build x labels for histograms
	list<string> add_ref_to_practice(string s){
		return practices collect (each.short_name+' '+s);
	}
	
	// generate the current histogram labels
	list<string> yearly_histo_labels{
		return first(practices).year_summary["Current year"] accumulate(add_ref_to_practice('y'+int(each)));
	}
	
	// build the histogram for a given indicator
	list<float> yearly_histo(string indicator){
		list<float> tmp;
		loop i from: 0 to: length(first(practices).year_summary["Current year"]){
			loop p over: practices{
				tmp <+ scale = "Total" ? p.year_summary[indicator][i] :  (p.year_summary[indicator][i] / p.year_summary["Crop area"][i]);
			}
		}
		return tmp;
	}
	
	// function to build y label, based on the scale choice (total or per ha)
	string y_label{
		return scale = "Total" ? "Total " + current_chart : current_chart + " per ha";
	}
	
	// compute the color of the bars of the histogram
	list histo_colors {
		list<rgb> cl <- practices collect (each.color_farmer);
		return first(practices).year_summary["Current year"] accumulate(cl);
	}
	
	// defines the visilibity of charts
	bool visibility{
		return current_chart != "Adoption rate" and current_chart != "Expenses details";
	}
	
	// returns the adoption rate
	list<list<float>> get_adoption_rate {
		if (scale = "Total"){
			return practices collect(each.year_summary["Crop area"]);
		}else{
			float total_area <- Plot sum_of (each.shape.area);
			list<list<float>> l <- [];
			loop p over: practices {
				l <+ p.year_summary["Crop area"] collect (each/total_area*100);
			}
			return l;
		}
		  
	}
} 


// ======================================================================
// experiments
// ======================================================================


// virtual generic experiment with common charts. All experiments inherit for this one, so it is not necessary to rewrite common charts
experiment generic_exp type:gui virtual: true {
	
	category "Economy" expanded: true color: rgb(243, 156, 18);
	parameter "Economy charts" var: economy_display_type among: ["Income","Details","Seasons summary"] category: "Economy";
	
	category "Practices comparison per year" expanded: true color: rgb(46, 204, 113);
	parameter "Chart" var: current_chart init: "Total crop biomass per practice" category: "Practices comparison per year" among: chart_list;
	parameter "Scale" var: scale init: "Total" category: "Practices comparison per year" among: ["Total","Per ha"];
	
	category "Weather and practices" expanded: true color: rgb(52, 152, 219);
	parameter "Show practice seasons" var: show_season category: "Weather and practices" among: possible_practices.keys+"None";
	parameter "Time range" var: time_range_type among: ["All","Last year","Last 3 years"] category: "Weather and practices";
	
	output {
		display base_map axes: false toolbar: false virtual: true{
			species species(plot_species);  
			species Farmer;
		}
		
		display base_practices_information type: 2d toolbar: false antialias: true virtual: true{
//			overlay position: {0, 0} size: {2000, 32#px} background: #white transparency: 0 rounded: false{
//				draw string("Seasons") at: {10 #px,10 #px}  anchor: #top_left color: #black font:overlay_font ; 
//            }

			chart histogram_info name: "" type: histogram y_label: y_label() visible: visibility(){
				datalist yearly_histo_labels() value: yearly_histo(current_chart)  color: histo_colors() ;
			}
			
			chart "Adoption rate"  type: histogram  visible: current_chart = "Adoption rate" y_label: scale = "Total" ? "Area per practice":"Area (%)"{
				datalist practices collect(each.short_name) value:  get_adoption_rate() color: practices collect(each.color_farmer) style: stack;
			}


			chart "Expenses details"  type: histogram  visible: current_chart = "Expenses details" y_label: scale = "Total" ? "Expenses per practice":"Expenses per ha"{
				datalist practices collect(each.short_name) value:  get_adoption_rate() color: practices collect(each.color_farmer) style: stack;
			}

			
			
//			chart "Test2" type: series {
//				data "data1" value: [3,4,2,4] color: #blue;
//				data "data1" value: [4,1,1,4] color: #red;
//			}

//			chart "Adoption rates" visible: (current_chart = "Adoption rates") type: series {
//				loop p over: practices.values {
//					data "Adoption of " + p.short_name value: (Farmer count (each.practice = p)) /length(Farmer) color: p.color_farmer marker: false thickness: 2;
//				}
//				
//			}
//
//			chart SeasonsChart name: "Seasons" visible: (current_chart = "Seasons") type: series  lines: false y_range: [0, max(2.5,length(practices.values)+0.5)] y_tick_values_visible: false x_range: time_range(time_range_type)  {
//				loop i from: 0 to: length(practices.values)-1 step: 1 {
//					data ""+i value: (i+0.5) color: #white style: area line_visible: false marker: false;
//					data practices.values[i].short_name+" seasons" value: (i+1)*int(practices.values[i].is_active_season) color: practices.values[i].color_farmer style: area line_visible: false marker: false;
//				}
//			}
//			
//			chart surfacePerPractice name: "Surface per practice" visible: (current_chart = "Surface per practice") type: series x_range: time_range(time_range_type)  {
//				loop i from: 0 to: length(practices.values)-1 step: 1 {
//					data practices.values[i].short_name+" biomass" value: get_cultivated_area_by_practice(practices.values[i]) color: practices.values[i].color_farmer marker: false;
//				}
//			}
//			
//			chart totalCropBiomass name: "Total crop biomass per practice" visible: (current_chart = "Total crop biomass per practice") type: series x_range: time_range(time_range_type)  {
//				loop i from: 0 to: length(practices.values)-1 step: 1 {
//					data practices.values[i].short_name+" biomass" value: get_total_biomass_by_practice(practices.values[i]) color: practices.values[i].color_farmer marker: false;
//				}
//			}
//			
//			chart AvgCropBiomass name: "Avg crop biomass per practice" visible: (current_chart = "Avg crop biomass per practice") type: series x_range: time_range(time_range_type)  {
//				loop i from: 0 to: length(practices.values)-1 step: 1 {
//					data practices.values[i].short_name+" biomass" value: get_avg_biomass_by_practice(practices.values[i]) color: practices.values[i].color_farmer marker: false;
//				}
//			} 

		}
		
		display base_economy type: 2d toolbar: false antialias: true virtual: true {
//			overlay position: {0, 0} size: {0.1, 0.1} background: #white transparency: 1 rounded: false{
//				draw string("Economy") at: {10 #px,10 #px}  anchor: #top_left color: #black font:overlay_font ; 
//            }

			
			chart "Income per ha" type: series size: {0.5,1} visible: economy_display_type = "Income" x_range: time_range(time_range_type) {
				loop p over: reverse(practices.values) {
					data "Mean income of " + p.short_name value: empty(Farmer where (each.practice = p))? 0.0 :((Farmer where (each.practice = p)) mean_of (each.day_revenue)) color: p.color_farmer style: bar;
				}
			}
			
			chart "Average yearly profit per ha" type: histogram size: {0.5,0.8}  position: {0.5,0.0} visible: economy_display_type = "Income"{
				loop p over: practices.values {
					data p.short_name value: empty(Farmer where (each.practice = p))? 0.0 :(((Farmer where (each.practice = p)) sum_of (each.money))/p.practice_area) color: p.color_farmer style: bar;
				}
			}
			
			chart "Income/expenses" type: series size: {0.5,1} visible: economy_display_type = "Details" x_range: time_range(time_range_type) {
				loop p over: reverse(practices.values) {
					data "Mean income of " + p.short_name value: empty(Farmer where (each.practice = p))? 0.0 :((Farmer where (each.practice = p)) mean_of (each.day_revenue)) color: p.color_farmer style: bar;
					data "Mean expenses of " + p.short_name value: empty(Farmer where (each.practice = p))? 0.0 :((Farmer where (each.practice = p)) mean_of (-each.day_expenses)) color: p.color_farmer style: bar;
				}
			}
			chart "Balance" type: series size: {0.5,1} position: {0.5,0.0} visible: economy_display_type = "Details" x_range: time_range(time_range_type) {
				loop p over: reverse(practices.values) {
					data "Mean income of " + p.short_name value: empty(Farmer where (each.practice = p))? 0.0 :((Farmer where (each.practice = p)) mean_of (each.money)) color: p.color_farmer style: line marker: false;				}
			}
			
			
			
//			chart "Total production" type: series size: {0.5,1} position: {0.5,0.0} visible: economy_display_type = "Details" x_range: time_range(time_range_type) {
//				loop p over: reverse(practices.values) {
//					data "Mean income of " + p.short_name value: empty(Farmer where (each.practice = p))? 0.0 :((Farmer where (each.practice = p)) mean_of (each.money)) color: p.color_farmer style: line marker: false;				}
//			}
			
		}
		
		display base_weather type: 2d toolbar: false antialias: true virtual: true {
//			overlay position: {0, 0} size: {2000, 32#px} background: #white transparency: 0 rounded: false{
//				draw string("Weather") at: {10 #px,10 #px}  anchor: #top_left color: #black font:overlay_font ; 
//            }

			chart "Temperature" type: series size: {0.5,0.5} x_range: time_range(time_range_type) {
				data "temperature min" value: the_weather.temp_min[current_date]  color: rgb(49, 130, 189) marker: false style: spline;
				data "temperature max" value: the_weather.temp_max[current_date]  color: rgb(230, 85, 13) marker: false style: spline;
				// season overlay
				loop i from: 0 to: length(practices.values)-1 step: 1 {					
					data practices.values[i].short_name+" seasons" legend: practices.values[i].short_name value: practices.values[i].activity collect(each*int(show_season = practices.keys[i])) color: rgb(practices.values[i].color_farmer,season_opacity) style: area line_visible: false marker: false use_second_y_axis: true;
				}
			}
			chart "Solar radiation" type: series size: {0.5,0.5} position: {0.5,0.0} x_range: time_range(time_range_type) {
				data "Solar radiation" value: the_weather.solar_radiation[current_date]  color: rgb(255, 191, 0) marker: false style: spline;
				// season overlay
				loop i from: 0 to: length(practices.values)-1 step: 1 {					
					data practices.values[i].short_name+" seasons" legend: practices.values[i].short_name value: practices.values[i].activity collect(each*int(show_season = practices.values[i].id)) color: rgb(practices.values[i].color_farmer,season_opacity) style: area line_visible: false marker: false use_second_y_axis: true;
				}
			}
			chart "Humidity" type: series size: {0.5,0.5} position: {0.0,0.5} x_range: time_range(time_range_type) {
				data "Humidity" value: the_weather.humidity[current_date]  color: rgb(102, 194, 255) marker: false style: spline ;
				// season overlay
				loop i from: 0 to: length(practices.values)-1 step: 1 {					
					data practices.values[i].short_name+" seasons" legend: practices.values[i].short_name value: practices.values[i].activity collect(each*int(show_season = practices.values[i].id)) color: rgb(practices.values[i].color_farmer,season_opacity) style: area line_visible: false marker: false use_second_y_axis: true;
				}
			}
			chart "Rainfall" type: series size: {0.5,0.5} position: {0.5,0.5} x_range: time_range(time_range_type) {
				data "Rainfall" value: the_weather.rainfall[current_date]  color: rgb(33, 113, 181) style: bar;
				// season overlay
				loop i from: 0 to: length(practices.values)-1 step: 1 {					
					data practices.values[i].short_name+" seasons" legend: practices.values[i].short_name value: practices.values[i].activity collect(each*int(show_season = practices.values[i].id)) color: rgb(practices.values[i].color_farmer,season_opacity) style: area line_visible: false marker: false use_second_y_axis: true;
				}
			}
		}
		

	} 
}


// simple experiment, wi
experiment "Simple experiment" type:gui parent: generic_exp {	
	
	output {
		layout value: horizontal([vertical([0::50,1::50])::50,vertical([2::50,3::50])::50]) tabs:false;
		
		display map axes: false toolbar: false parent: base_map{
		
//			agents pest value: (plot_species != Plot?species("Parasite"):nil);	
//			agents predator value: (plot_species != Plot?species("Predator"):nil);
		}

		
		display pc name: "Practices comparison per year" type: 2d toolbar: false antialias: true parent: base_practices_information{}
		
		display Economy type: 2d toolbar: false antialias: true parent: base_economy{}
		
		display Weather type: 2d toolbar: false antialias: true parent: base_weather{}
		
	} 
}



